; This is a description of bconf's grammar using ABNF as defined in RFC 5234 (https://datatracker.ietf.org/doc/html/rfc5234),
; with case-sensitivity support from RFC 7405 (https://datatracker.ietf.org/doc/html/rfc7405).
; 
; bconf documents are expected to be UTF-8 encoded. Some grammar rules in this document, such as
; allowed characters in strings and comments, refer to their Unicode Code Points.
;
; Unless specified otherwise, white space is largely negligible and can be used for formatting,
; denoted by the OWSP rule (meaning optional white space). White space that is significant
; will be denoted with the WSP rule.
;
; Supporting documents and comments should be consulted for a full implementation. While
; core features such as `import`, `extends` and `exports` conform to the grammar, they are
; defined by their behaviour and specific implementation.

bconf		= OWSP [ LBRACE ] [ OWSP ] [ item *( block-sep OWSP item ) [ block-sep ] ] [ OWSP ] [ RBRACE ] OWSP
item		= key-value / statement / key / comment
block-sep	= OWSP ( SEMICOLON / NEWLINE ) OWSP

; -----------------------
; Core Statements
; -----------------------
key-value		= key OWSP ( "=" / "<<" ) OWSP value
key-value		=/ key OWSP block
statement		= key 1*WSP 1*( statement-item [ 1*WSP ] )
statement-item	= value / variable-path / alphanumeric-key
comment			= "//" *( comment-char )

; -----------------------
; Values & Data Structures
; -----------------------
value			= primitive / block / array / modifier / variable-key
primitive		= string / number / bool / null
modifier		= alphanumeric-key LPAREN OWSP ( value / key ) OWSP RPAREN
block			= LBRACE OWSP [ item *( ( SEMICOLON / NEWLINE ) OWSP item ) [ block-sep ] ] OWSP RBRACE
array			= LBRACKET OWSP [ value *( array-sep value ) [ array-sep ] ] OWSP RBRACKET
array-sep		= OWSP COMMA OWSP

; -----------------------
; Keys & Variables
; -----------------------
key					= key-path / variable-path
key-path			= base-key *( ( PERIOD base-key ) / *( array-accessor ) )
base-key			= alphanumeric-key / singleline-string
alphanumeric-key	= 1*( ALPHA / DIGIT / HYPHEN-MINUS / UNDERSCORE )
array-accessor		= LBRACKET OWSP dec-int OWSP RBRACKET
variable-key		= DOLLARSIGN alphanumeric-key
variable-path		= variable-key *( ( PERIOD base-key ) / *( array-accessor ) )

; -----------------------
; Primitives
; -----------------------
string				= multiline-string / singleline-string
singleline-string	= DQUOTE *( string-char / escaped-char / embedded-value ) DQUOTE
multiline-string	= 3DQUOTE *( multiline-char / escaped-char / embedded-value ) 3DQUOTE

number			= float / integer
number-prefix	= HYPHEN-MINUS / PLUS
integer			= [ number-prefix ] dec-int
float			= [ number-prefix ] ( ( dec-int PERIOD [ dec-int ] [ exponent ] ) / ( dec-int exponent ) )
dec-int			= DIGIT *( [ UNDERSCORE ] DIGIT )
exponent		= "e" integer

bool			= %s"true" / %s"false"
null			= %s"null"

; -----------------------
; Allowed Characters
; -----------------------
comment-char	= HTAB / %x20-7E / %xA0-D7FF / %xE000-10FFFF  ; Tab + printable chars, no controls
string-char		= %x20-21 / %x23 / %x25-5B / %x5D-7E / %xA0-D7FF / %xE000-10FFFF ; Printable chars, no control chars or special chars ", \, $
multiline-char	= HTAB / string-char / NEWLINE ; Tab + printables + newlines. Quotation marks must still be escaped

embedded-value	= DOLLARSIGN LBRACE OWSP ( primitive / variable-path / modifier ) OWSP RBRACE
escaped-char		= "\" ( 
	%x22 /			; "				quotation mark
	%x5C /			; \				backslash
	%x24 /			; $				dollar sign
	%x62 /			; b				backspace
	%x66 /			; f				form feed
	%x6E /			; n				new line
	%x72 /			; r				carriage return
	%x74 /			; t				tab
	%x75 4HEXDIG /	; uXXXX			U+XXXX
	%x55 8HEXDIG	; UXXXXXXXX		U+XXXXXXXX
)

; -----------------------
; BConf Common Symbols & Whitespace
; -----------------------
NEWLINE			= LF / CRLF
OWSP			= *( WSP / NEWLINE ) ; Optional white space. Different to WSP, this denotes white space that carries no syntactical significance
LBRACE			= "{"
RBRACE			= "}"
LBRACKET		= "["
RBRACKET		= "]"
LPAREN			= "("
RPAREN			= ")"
COMMA			= ","
SEMICOLON		= ";"
PERIOD			= "."
DOLLARSIGN		= "$"
HYPHEN-MINUS	= "-"
PLUS			= "+"
UNDERSCORE 		= "_"

; -----------------------
; Core ABNF Rules (included for clarity)
; -----------------------
ALPHA		= %x41-5A / %x61-7A		; A-Z / a-z
CR			= %x0D					; carriage return
CRLF		= CR LF					; internet standard newline
DIGIT		= %x30-39				; 0-9
DQUOTE		= %x22					; " (double quote)
HEXDIG		= DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
HTAB		= %x09					; horizontal tab
LF			= %x0A					; linefeed
SP			= %x20					; space
WSP			= SP / HTAB				; white space